If(
    // If there are no citations
    CountRows(Topic.Answer.Text.CitationSources) = 0, 
    // Render the original answer
    Topic.Answer.Text.MarkdownContent,

    // If there are citations, rebuild the Markdown answer entirely 
    Topic.Answer.Text.Content & Char(10) & Char(10) & 

    // Concatenate citations in Markdown format
    Concat(
        Topic.Answer.Text.CitationSources,
        
        // Recreate the Url if there isn't one
        "[" & Id & "]: " & 
        If(
            // If there is no URL
            IsBlank(Url),
            If(
                Left(Name,8) = "https://", 
                Substitute(Name, " ", "%20"),
                "cite:" & Id
            ),
            // Else use the returned URL Value.
            Url
        ) &
         With(
  {
    firstPos: Find("value=", Text) + 7, // we add 7 to make up for value="
    lastPos: Find(">", Text, 1) // find the closing bracket
  },
  // only add "#page=<page number>" if brackets are found and document is pdf
    If(
      firstPos > 0 && lastPos > 0 && EndsWith(Lower(Name), ".pdf"),
      
      "#" & "page=" & Mid(Text, firstPos, (lastPos - firstPos) - 1),
      "" //add nothing if not found 
  )
)
&
        
        // Improve file name formatting
        " """ & 

        // Extract the file name, remove query if present
        Substitute(
            If(
                Find("?", Last(Split(Name, "/")).Value) > 0,
                Left(Last(Split(Name, "/")).Value, Find("?", Last(Split(Name, "/")).Value) - 1),
                Last(Split(Name, "/")).Value
            ),
            "%20", " "
        )

        &  """",

        // Line breaks between citations
        Char(10) & Char(10)
    )
)
